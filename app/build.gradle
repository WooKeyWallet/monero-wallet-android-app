apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def computeVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    def trim = cmd.execute().text.trim()
    if (trim == null || trim.length() == 0) {
        trim = 1
    }
    trim.toInteger()
}

def gitVersionTag() {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    def pattern = "-(\\d+)-g"
    def matcher = version =~ pattern

    if (matcher) {
        version = version.substring(0, matcher.start()) + "." + matcher[0][1]
    } else {
        version = version + ".0"
    }

    return version
}

def computeVersionName() {
    return "1.0.1"
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.wookey.wallet"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode computeVersionCode()
        versionName computeVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' D'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'arm64-v8a', 'armeabi-v7a'
            universalApk true
        }
    }

    androidExtensions {
        experimental = true
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def abiName = output.getFilter(com.android.build.OutputFile.ABI)
            if (abiName == null) abiName = "universal"
            outputFileName = "wookey-${variant.versionName}-${variant.versionCode}-${abiName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation "com.android.support:design:28.0.0"

    // 架构组件
    def lifecycle_version = "1.1.1"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation "android.arch.lifecycle:reactivestreams:$lifecycle_version"
    implementation "android.arch.persistence.room:runtime:$lifecycle_version"
    kapt "android.arch.persistence.room:compiler:$lifecycle_version"

    implementation "com.github.lygttpod:SuperTextView:2.1.8"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.0"

    implementation 'com.google.android:flexbox:1.0.0'

    // 二维码扫描
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.4'

    // 日期选择
    implementation 'com.contrarywind:Android-PickerView:4.1.7'

    implementation project(':monero')

}
